@Library("jenlib") _

import org.electronicvisions.jenlib.ShellManipulator

final String PROJECT_NAME = "model-hw-mc-genetic"

try {
	String virtualEnvPath = null

	// deap is not yet in the latest container -> TODO: use default pipeline once deap is in container
	stage("Prepare venv") {
		runOnSlave(label: "frontend") {
			inSingularity(app: "dls") {
				virtualEnvPath = "${WORKSPACE}/venv"
				jesh("virtualenv --system-site-packages ${virtualEnvPath}")

				inVirtualEnv(virtualEnvPath) {
					jesh("pip install deap")
				}
			}
		}
	}

	// Cannot use wafDefaultPipeline with venv, because venv needs to be activated inside the container:
	//   * venv does not fill the SINGULARITY prefixed environment variables
	//   * our singularity container overrides $PYTHONHOME
	withCcache() {
		inSingularity(app: "dls") {
			inVirtualEnv(virtualEnvPath) {
				withWaf() {
					wafSetup(projects: ["${PROJECT_NAME}"])

					stage("Build") {
						onSlurmResource(partition: "jenkins", "cpus-per-task": "8") {
							jesh("waf configure install --test-execnone")
						}
					}

					stage("Tests") {
						onSlurmResource(partition: "cube", wafer: 62, "fpga-without-aout": 3) {
							configureHxCubeBitfile()
							jesh("waf build --test-execall -j1")
						}
					}
				}
			}
		}
	}

	stage("Test Evaluation") {
		runOnSlave(label: "frontend") {
			String xmlResultPattern = "build/test_results/**/*.xml"
			archiveArtifacts xmlResultPattern

			step([$class       : 'XUnitPublisher',
			      thresholdMode: 1,
			      thresholds   : [[$class           : 'FailedThreshold',
			                       unstableThreshold: '0'],
			      ],
			      tools        : [[$class               : 'GoogleTestType',
			                       deleteOutputFiles    : true,
			                       failIfNotNew         : true,
			                       pattern              : xmlResultPattern,
			                       skipNoTestFiles      : false,
			                       stopProcessingIfError: true]
			      ]
			])
		}
	}

	stage("Linting Warnings") {
		runOnSlave(label: "frontend") {
			recordIssues(qualityGates: [[threshold: 1,
										 type     : 'TOTAL',
										 unstable : true]],
						 blameDisabled: true,
						 skipPublishingChecks: true,
						 tools: [pyLint(pattern: "build/test_results/**/*.pylint",
										id: "pylint_" + UUID.randomUUID().toString(),
										name: "Pylint Warnings"),
								 pep8(pattern: "build/test_results//**/*.pycodestyle",
									  id: "pep8_" + UUID.randomUUID().toString(),
									  name: "PEP8 Warnings")]
			)
		}
	}

} catch (Throwable t) {
	notifyFailure(mattermostChannel: "#jjk-notifications")
	throw t
}

if (currentBuild.currentResult != "SUCCESS") {
	notifyFailure(mattermostChannel: "#jjk-notifications")
}


void inVirtualEnv(String path, Closure content) {
	ShellManipulator manipulator = ShellManipulator.fromEnvironment(this)
	manipulator.add(". ${path}/bin/activate && ", "")

	try {
		content()
	} catch (Throwable anything) {
		throw anything
	} finally {
		manipulator.restore()
	}
}
